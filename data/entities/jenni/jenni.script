#include "scripts\base.inc"

global WinMenu;

#include "interface\verbbar\verbbar_functions.inc"

var verb_default = "Talk to";

var Jenni;

Jenni = Scene.GetNode("jenni");

Jenni.SubtitlesPosX = 0; //position of subtitles for your entity
Jenni.SubtitlesPosY = -20;


////////////////////////////////////////////////////////////////////////////////
on "LookAt"
{
  verb_setBar(Command.Verb + " "+self.Caption);
  GoToObject();
  Game.Interactive=false;

  actor.Talk("She looks pretty busy");

  verb_cleanBar();
  Game.Interactive=true;
}

////////////////////////////////////////////////////////////////////////////////
on "TalkTo"
{
  verb_setBar(Command.Verb + " "+self.Caption);

  var Responses;
  var Selected;
  GoToObject();

  Game.Interactive = false;
  
  verb_changeMode(1);

  var EndBranch = false;

  while(!EndBranch) // loop until the player ends this dialogue
  {  
    Game.AddResponseOnce(0, "What is this place?");
    Game.AddResponseOnce(2, "What are you doing?");
    Game.AddResponse(3, "I'll let you get back to your work.");
   
    var Res = Game.GetResponse(true); // automatically select last response
    
    actor.Talk(Game.LastResponse);
    
    // handle the responses
    switch(Res)
    {
      case 0:
        Jenni.Talk("It's Doctor Anenome's laboratory.");
        Game.AddResponseOnce(1, "Who is Doctor Anenome?");
        break;
      case 1:
        Jenni.Talk("Dr. Anenome is the leader in bio modification surgery.");
        break;        
      case 2:
        Jenni.Talk("To tell you the truth, I'm just trying to look busy.");
        Jenni.Talk("The computer is giving me problems today.");
        break;        
      case 3:
        EndBranch = true;  // response #3 exits the dialogue
        break;        
    }
  }

  verb_cleanBar();
  verb_changeMode(0);

  Game.Interactive = true;
}

////////////////////////////////////////////////////////////////////////////////
on "letter"
{
  verb_setBar(Command.Verb + " " + Command.Object1_caption + " with " + Command.Object2_caption);
  GoToObject();
  Game.Interactive=false;

  actor.Talk("I got this letter in the mail.");
  Jenni.Talk("Ah, fantastic.");
  Jenni.Talk("Go right in. Dr. Anenome is right in the next room.");
  verb_cleanBar();
  Game.Interactive=true;
  Game.ChangeScene("scenes\ending\ending.scene");
}

////////////////////////////////////////////////////////////////////////////////
on "LeftClick"
{
  if(Command.Verb == "")
  {
    GoToObject();
  }
}

////////////////////////////////////////////////////////////////////////////////
on "RightClick"
{
  verb_apply(verb_default);
}

////////////////////////////////////////////////////////////////////////////////
on "MouseEntry"
{
  verb_hover(verb_default);
}

////////////////////////////////////////////////////////////////////////////////
on "MouseLeave"
{
  verb_unhover(verb_default);
}

function GoToObject()
{
  actor.GoTo(59, 50);
  actor.TurnTo(DI_UPRIGHT);
}
